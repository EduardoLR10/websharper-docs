# Unit testing

The `WebSharper.Testing` library (available on [NuGet](https://www.nuget.org/packages/websharper.testing)) exposes a clean F# syntax to write unit tests for libraries, services and websites.
It is a wrapper around `QUnit` for presentation which provides transparent handling of synchronous and asynchronous expressions, and generating random data for property testing.
The tests page is meant to be ran in a browser, but the server-side of a website can be tested too by making remote calls from the tests.

All functionality within are accessible with the `WebSharper.Testing` namespace.
Code samples below are also assuming that the module or assembly containing them is annotated with `[<JavaScript>]` except
when referring to being server-side code.

## Contents

- [Categories and tests](#categories-and-tests): organizing and running tests
    - [TestCategory](#testcategory)
    - [Test](#test)
    - [Expect](#expect)
    - [Runner.RunTests](#runnerruntests)
- [Assertions](#assertions): simple assertions
- [Equality checks](#equality-checks): equality/non-equality checkers
- [Property testing](#property-testing): property testing with random values
- [Exception testing](#exception-testing): check for a raised exception
- [Looping](#looping): registering multiple tests with a loop
- [Asynchronous tests](#asynchronous-tests): using `Async` values inside test workflows

## Categories and tests

### TestCategory

Creates a definition for a single named test category.
You can think of it as similar to F#'s `lazy` keyword: the code body inside it is not executed immediately,
but when only when it will be passed to `Runner.RunTests`.
So the returned value (of type `TestCategory`) need to be assigned to local or module variable if you want to run it later in your project.

#### Example:

```fsharp
let MyTests =
    TestCategory "MyTests" {
        Test "Equality" {
            equal 1 1
        }
    }
```

### Test

Creates a definition for a single named test.
Similar to `TestCategory`, it delays the execution of its content.
Does not return anything, but registers tests in `QUnit`.
Do not evaluate it in a module context if you want the test to be under a category reliably.

#### Example:

```fsharp
let EqualityTests() =
    Test "Equality" {
        equal 1 1
        notEqual 1 2
    }
```

### Runner.RunTests

Takes an array of `TestCategory` values
Returns an `IControlBody`, which can be used inside the `client <@ ... @>` helper to serve a page that runs the given tests,
or call its `ReplaceInDom` method directly on the client, to replace a placeholder DOM element to the content generated by `QUnit`.

#### Example:

```fsharp
let RunAllTests() =
    Runner.RunTests [|
        MyTests
    |]

// later, in server-side code, serve this on a [Sitelet](sitelets.md) endpoint:
    Content.Page(
        Title = "Unit tests",
        Body = client <@ RunAllTests() @>
    )
```

### Expect

Tells the testing framework, how many test cases are expected to be registered for this category.
If the category would register no tests, `QUnit` reports it as a failed test case, unless you add `expect 0`.

#### Example:

```fsharp
let MyTests =
    TestCategory "MyTests" {
        Test "Equality" {
            expect 2
            equal 1 1
            notEqual 1 2
        }
        Test "Not failing" {
            expect 0
            // a call to some outside function:
            doNotFail() 
            // if this throws an error, that still fails the test case, otherwise ok
        }
    }
```

## Assertions

### isTrue, isTrueMsg, isTrueAsync, isTrueMsgAsync

Checks if a single argument expression evaluates to `true`.
Versions with `Msg` also accept a name for that single assertion.
Versions with `Async` accept a value of type `Async<bool>` instead and runs it as an asynchronous test.

#### Example:

```fsharp
    Test "Equality" {
        isTrue (1 = 1)
        isTrueMsg (1 = 1) "One equals one"
        isTrueAsync (async { return 1 = 1 }) 
        isTrueMsgAsync (async { return 1 = 1 }) "One equals one async version"
    }
```
